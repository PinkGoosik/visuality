plugins {
	id 'fabric-loom' version '0.11-SNAPSHOT'
	id 'maven-publish'
	id 'io.github.juuxel.loom-quiltflower' version '1.6.0'
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	maven { url "https://api.modrinth.com/maven" }
	maven { url "https://maven.pinkgoosik.ru" } //cosmetica, goosik-config
	maven { url "https://maven.terraformersmc.com" } //modmenu
	maven { url "https://maven.parchmentmc.org" } //parchment
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.layered() {
		officialMojangMappings()
		parchment("org.parchmentmc.data:parchment-1.18.1:2022.02.13@zip")
	}
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	include modImplementation("ru.pinkgoosik:goosik-config:0.2.0-1.17.1-fabric")
	include modImplementation("ru.pinkgoosik:cosmetica:0.4.0")
	
	modLocalRuntime "com.terraformersmc:modmenu:3.0.1"
	modLocalRuntime "maven.modrinth:lazydfu:0.1.2"
}

processResources {
	inputs.property "version", project.mod_version
	filesMatching("fabric.mod.json") {
		expand "version": project.mod_version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
	repositories {
		maven {
			url "https://maven.pinkgoosik.ru/"
			credentials {
				username System.getenv("MAVEN_USER")
				password System.getenv("MAVEN_PASS")
			}
		}
	}
}
